CreationTool:
    objects *= Object
    sections += Section
    (player = Player)?
    'start' start = [Section]
    'finish' finish = [Section]
;

Section:
    '(' name = CTID ')' '{'
        properties *= SectionProperty
    '}'
;

Object:
    '[' name = CTID ']' '{'
        properties *= CommonProperty
    '}'
;

Player:
    'Player' '{'
        properties *= PlayerProperty
    '}'
;

CommonProperty:
    DescriptionProperty | ContainsProperty | RequirementsProperty | ActionsProperty
;

SectionProperty:
    CommonProperty | LinkProperty | EventsProperty
;

PlayerProperty:
    InventoryProperty | HealthProperty | ScoreProperty
;

DescriptionProperty:
    'description' description = STRING
;

ContainsProperty:
    'contains' contains *= [Object][',']
;

RequirementsProperty:
    'require' requirements *= [Object][',']
;

LinkProperty:
    direction = Direction link = [Section]
;

InventoryProperty:
    'inventory' inventory *= [Object][',']
;

HealthProperty:
    'health' health = INT
;

ScoreProperty:
    'score' score = INT
;

ActionsProperty:
    'actions' actions *= Action
;

EventsProperty:
    'events' events *= Event
;

Action:
    '(' name = CTID ')' '{'
        preconditions *= Precondition
        effects *= Effect
    '}'
;

Event:
    '(' name = CTID ')' '{'
        conditions *= Condition
        actions *= Action
    '}'
;

Precondition:
    'require' require = [Object]
;

Effect:
    'change' property = EffectProperty
;

EffectProperty:
    'description' description = STRING | 'score' score = INT | 'health' health = INT
;

Condition:
    'require' require = [Object] | 'score' scoreComparison = ComparisonOperator INT
;

ComparisonOperator:
    '>' | '>=' | '<' | '<=' | '==' | '!='
;

Direction:
    'N' | 'S' | 'E' | 'W' | 'NE' | 'NW' | 'SE' | 'SW'
;

Keyword:
    'begin' | 'end' | 'start' | 'finish' | 'name' | 'description' | 'object' | 'link' |
    'require' | 'open' | 'go' | 'look' | 'take' | 'drop' | 'health' | 'score' | 'actions' |
    'events' | 'change'
;

CTID:
    !Keyword ID
;

Comment:
    /\/\*(.|\n)*?\*\//
;
